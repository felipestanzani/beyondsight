plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'io.freefair.lombok' version '9.0.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.felipestanzani'
version = '0.0.1-SNAPSHOT'
description = 'BeyondSight - code dependency analyzer'

java {
    sourceCompatibility = JavaVersion.VERSION_25
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

repositories {
	mavenCentral()
	maven {url 'https://repo.spring.io/milestone' }
    maven {url 'https://download.eclipse.org/releases/2025-06' }
    maven {url 'https://download.eclipse.org/eclipse/updates/4.32' }
    maven {url 'https://download.eclipse.org/tools/orbit/simrel/orbit-aggregation/release/4.32.0' }
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation 'org.eclipse.platform:org.eclipse.osgi:3.23.200'
    implementation 'org.eclipse.platform:org.eclipse.core.resources:3.21.0'
    implementation 'org.eclipse.platform:org.eclipse.core.runtime:3.34.0'
    implementation 'org.eclipse.platform:org.eclipse.core.jobs:3.15.0'
    implementation 'org.eclipse.jdt:org.eclipse.jdt.core:3.43.0'

    implementation 'org.springframework.data:spring-data-neo4j'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework:spring-webmvc:6.2.12'
    implementation 'org.springframework.ai:spring-ai-starter-mcp-server:1.0.3'
    implementation 'org.springframework.ai:spring-ai-starter-mcp-server-webmvc:1.0.3'
	implementation 'org.aspectj:aspectjrt:1.9.22'
	implementation 'org.aspectj:aspectjweaver:1.9.22'
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.27.1'
    implementation 'tools.jackson.core:jackson-databind:3.0.2'
    implementation 'com.felipestanzani:jtoon:0.1.2'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

shadowJar {
    archiveClassifier = ''
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'com.mcp.McpServer'
    }
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
}

tasks.register('printJar') {
    dependsOn shadowJar
    doLast {
        def jar = shadowJar.archiveFile.get().asFile
        println ""
        println "Build complete!"
        println "JAR: ${jar.absolutePath}"
        println "Run: java -javaagent:lombok.jar -jar ${jar.name} --workspace=/path/to/project"
        println ""
    }
}

defaultTasks 'clean', 'shadowJar', 'printJar'

bootJar {
    enabled = false
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}
